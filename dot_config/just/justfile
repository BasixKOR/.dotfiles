#!/usr/bin/env just --justfile
justfile := justfile()

import '~/.justfile'

_list:
  #!/usr/bin/env bash
  if command -v fzf &>/dev/null; then
    just --choose --justfile {{justfile}}
  else
    just --list --justfile {{justfile}}
  fi

[no-cd]
yarn-extract:
  yarn set version from sources --branch @yarnpkg/cli/{{ shell("yarn -v") }} --no-minify

# https://apple.stackexchange.com/a/466029/460194
macos-enable-sudo-touchid:
  sed "s/^#auth/auth/" /etc/pam.d/sudo_local.template | sudo tee /etc/pam.d/sudo_local

kill-ios-simulator:
  sudo killall -9 com.apple.CoreSimulator.CoreSimulatorService

launchctl-update-path:
  sudo launchctl config user path {{env("PATH")}}

ip:
  curl https://ipinfo.io/ | yq -P -oy .

[no-cd]
contributors:
  @git log --format='%aN' | sort -u

[no-cd]
big-folder:
  du -hs */ | sort -hr | head

[no-cd]
git-branch-purge:
  git branch | cut -c 3- | gum choose --no-limit | xargs git branch -Dr

curl-measure *ARGS:
  curl -w @"{{ justfile_directory() }}/curl-format.txt" -o /dev/null {{ ARGS }} 
