#!/usr/bin/env just --justfile
justfile := justfile()

import '~/.justfile'

import 'macos.just'

_list:
  #!/usr/bin/env bash
  if command -v fzf &>/dev/null; then
    just --choose --justfile {{justfile}}
  else
    just --list --justfile {{justfile}}
  fi

[no-cd]
yarn-extract:
  yarn set version from sources --branch @yarnpkg/cli/{{ shell("yarn -v") }} --no-minify

ip:
  curl https://ipinfo.io/ | yq -P -oy .

[no-cd]
contributors:
  @git log --format='%aN' | sort -u

[no-cd]
big-folder:
  du -hs */ | sort -hr | head

[no-cd]
git-branch-purge:
  git branch | cut -c 3- | gum choose --no-limit | xargs git branch -Dr

curl-measure *ARGS:
  curl -w @"{{ justfile_directory() }}/curl-format.txt" -o /dev/null {{ ARGS }} 

[no-cd]
nr:
  #!/bin/bash
  COMMAND="$(command -v ny || command -v dum || echo "You should have krzkaczor/ny or egoist/dum installed!"; exit 1)"
  SCRIPT="$(jq -cr '.scripts | keys_unsorted | .[]' package.json | fzf --tac --height=~20 --preview 'jq --arg v {} -r '\''.scripts.[$v]'\'' package.json')"
  $COMMAND "$SCRIPT"

[no-cd]
todo:
  rg '(TODO|FIXME)(\([^)]+\))?:'

amp *args:
  amp --mcp-config "$(fd -g '*.json' ~/.config/amp/mcps | fzf -m -d / --with-nth -1 --preview 'bat --color always --style plain -l json {}' | xargs jq -cs add)" {{args}}
